plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.22.1'
    //    id 'org.beryx.runtime' version '1.11.3'
}

group = 'notepack'
version = '0.8'
description 'Privacy oriented, without vendor lock in note organizer desktop application.'

sourceCompatibility = 1.9
targetCompatibility = 1.9
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


repositories {
        
    maven { url "https://repo.maven.apache.org/maven2" }
}

javafx {
    modules = [ 'javafx.controls' , 'javafx.fxml', 'javafx.web']
}

dependencies {
    compile group: 'org.openjfx', name: 'javafx-base', version:'14'
    compile group: 'org.openjfx', name: 'javafx-controls', version:'14'
    compile group: 'org.openjfx', name: 'javafx-fxml', version:'14'
    compile group: 'org.openjfx', name: 'javafx-web', version:'14'
    compile group: 'org.json', name: 'json', version:'20190722'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version:'1.64'
    compile group: 'org.bouncycastle', name: 'bcpg-jdk15on', version:'1.64'
    //    compile group: 'com.atlassian.commonmark', name: 'commonmark', version:'0.14.0'
    compile 'com.vladsch.flexmark:flexmark-all:0.62.2'

}
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--ignore-signing-information']
    launcher {
        name = 'Notepack'
        jvmArgs = ['-Xms256m', '-Xmx2g']
    }    
    jpackage {
        installerOptions += [
                '--description', project.description,
                '--app-version', version,
                '--copyright', 'Ogefest',
                '--license-file', 'LICENSE',
                '--vendor', 'Ogefest'
        ]        
        installerType = project.findProperty('installerType')
        
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'gfx/logo_64.ico']
            installerOptions += [
                '--resource-dir', "build/Notepack-package/Notepack/lib",
                '--win-per-user-install', '--win-dir-chooser',
                '--win-menu', '--win-shortcut'
            ]
        }        
        
        if (installerType in ['deb', 'rpm']) {
            imageOptions += ['--icon', 'gfx/logo_256.png']
            installerOptions += [
                '--resource-dir', "build/Notepack-package/Notepack/lib",
                '--linux-menu-group', 'Office',
                '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                '--linux-deb-maintainer', 'hello@notepackapp.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                '--linux-rpm-license-type', 'MPLv2'
            ]
        }

        outputDir = 'Notepack-package'
        imageName = 'Notepack'
	icon = 'gfx/logo_128.png'
        skipInstaller = false
        installerName = 'Notepack'
    }

}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}

mainClassName = 'notepack.Main'
