plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.22.1'
    //    id 'org.beryx.runtime' version '1.11.3'
}

group = 'notepack'
version = '0.9.6'
description 'Privacy oriented, without vendor lock in note organizer desktop application.'

sourceCompatibility = 1.9
targetCompatibility = 1.9
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


repositories {
        
    maven { url "https://repo.maven.apache.org/maven2" }
}

javafx {
    modules = [ 'javafx.controls' , 'javafx.fxml', 'javafx.web']
}

dependencies {
    compile group: 'org.openjfx', name: 'javafx-base', version:'14'
    compile group: 'org.openjfx', name: 'javafx-controls', version:'14'
    compile group: 'org.openjfx', name: 'javafx-fxml', version:'14'
    compile group: 'org.openjfx', name: 'javafx-web', version:'14'
    compile group: 'org.json', name: 'json', version:'20190722'
    compile 'com.vladsch.flexmark:flexmark-all:0.62.2'
    compile group: 'net.lingala.zip4j', name: 'zip4j', version: '2.6.4'
    compile group: 'org.mnode.ical4j', name: 'ical4j', version: '3.0.21'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-webdav', version: '2.21.5'

}

configurations {
    all {
        exclude module: 'commons-logging'
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--ignore-signing-information']
    addExtraDependencies("javafx")
    launcher {
        name = 'Notepack'
        jvmArgs = ['-Xms256m', '-Xmx2g']
    }
    mergedModule {
        additive = true
        uses 'net.fortuna.ical4j.validate.CalendarValidatorFactory'

    }
    jpackage {
        installerOptions += [
                '--description', project.description,
                '--app-version', version,
                '--copyright', 'Ogefest',
                '--license-file', 'LICENSE',
                '--vendor', 'Ogefest'
        ]        
        installerType = project.findProperty('installerType')
        
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'gfx/logo_64.ico']
            installerOptions += [
                '--resource-dir', "build/Notepack-package/Notepack/lib",
                '--win-per-user-install', '--win-dir-chooser',
                '--win-menu', '--win-shortcut'
            ]
        }        
        
        if (installerType in ['deb', 'rpm']) {
            imageOptions += ['--icon', 'gfx/logo_256.png']
            installerOptions += [
                '--resource-dir', "build/Notepack-package/Notepack/lib",
                '--linux-menu-group', 'Office',
                '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                '--linux-deb-maintainer', 'hello@notepackapp.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                '--linux-rpm-license-type', 'MPLv2'
            ]
        }

        outputDir = 'Notepack-package'
        imageName = 'Notepack'
        skipInstaller = false
        installerName = 'Notepack'
    }

}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}

mainClassName = 'notepack.Main'
